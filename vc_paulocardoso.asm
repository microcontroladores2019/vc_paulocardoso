	ORG 0X00
DIGA EQU 0x30
DIGB EQU 0x31
DIGC EQU 0x32
DIGD EQU 0x33
CONT EQU 0x20
BUT EQU 0x21
W_TEMP EQU 0x22
STATUS_TEMP EQU 0x23
PCLATH_TEMP EQU 0x24

	GOTO SETTINGS

	ORG 0X04
	
	MOVWF W_TEMP ;Copy W to TEMP register
	SWAPF STATUS,W ;Swap status to be saved into W
	CLRF STATUS ;bank 0, regardless of current bank, Clears IRP,RP1,RP0
	MOVWF STATUS_TEMP ;Save status to bank zero STATUS_TEMP register
	MOVF PCLATH, W ;Only required if using pages 1, 2 and/or 3
	MOVWF PCLATH_TEMP ;Save PCLATH into W
	CLRF PCLATH ;Page zero, regardless of current page
	
	BTFSC INTCON,RBIF
	GOTO CLEAR
	BTFSC INTCON,INTF
	COMF BUT,F ; Ativa o loop de pausa
	BTFSS BUT,0 ; PAUSA A CONTAGEM SE BUT0 = 1
	DECFSZ CONT,F
	GOTO NOT_YET
	GOTO CONT_SEG
CLEAR:
	CLRF DIGA
	CLRF DIGB
	CLRF DIGC
	CLRF DIGD
	MOVLW 26
	MOVWF CONT	
	GOTO NOT_YET

CONT_SEG:
	MOVLW 26
	MOVWF CONT
	INCF DIGA,F
	MOVLW 10
	SUBWF DIGA,W
	BTFSS STATUS,Z
	GOTO NOT_YET
	CLRF DIGA
	INCF DIGB,F
	MOVLW 10
	SUBWF DIGB,W
	BTFSS STATUS,Z
	GOTO NOT_YET
	CLRF DIGB
	INCF DIGC,F
	MOVLW 10
	SUBWF DIGC,W
	BTFSS STATUS,Z
	GOTO NOT_YET
	CLRF DIGD
	INCF DIGD,F
	MOVLW 10
	SUBWF DIGD,W
	BTFSS STATUS,Z
	GOTO NOT_YET
	CLRF DIGD

NOT_YET: 
	MOVLW 106
	MOVWF TMR0


	MOVF PCLATH_TEMP, W ;Restore PCLATH
	MOVWF PCLATH ;Move W into PCLATH
	SWAPF STATUS_TEMP,W ;Swap STATUS_TEMP register into W 
	;(sets bank to original state)
	MOVWF STATUS ;Move W into STATUS register
	SWAPF W_TEMP,F ;Swap W_TEMP
	SWAPF W_TEMP,W ;Swap W_TEMP into W
	BCF INTCON,INTF ; ZERA A FLAG DE B0
	BCF INTCON,TMR0IF ; ZERA FLAG DO TIMER0
	BCF INTCON,RBIF	  ; ZERA FLAG PORTB(4-7)
	RETFIE

SETTINGS:
	BSF INTCON,5  ;ATIVA INTERRUPT DO TIMER0
	BSF INTCON,4  ;ATIVA INTERRUPT PORTB0
	BSF INTCON,3  ;ATIVA INTERRUPT PORTB(4-7)
	BSF STATUS,RP0
	BSF OPTION_REG,6  ;O INTERRUPT DE B0 SERÁ NA BORDA DE SUBIDA
	BCF OPTION_REG,5 ; ATIVA O TIMER0
	BCF OPTION_REG,3 ; COLOCA O PRESCALER NO TIMER0
	BSF OPTION_REG,2
	BSF OPTION_REG,1
	BSF OPTION_REG,0 ; COLOCA PRESCALER DE 1:256
	MOVLW TRISA 
	MOVWF FSR
	CLRF INDF  ;COLOCA PORTA COMO OUTPUT
	MOVLW TRISD 
	MOVWF FSR
	CLRF INDF  ;COLOCA PORTD COMO OUTPUT
	MOVLW TRISC
	MOVWF FSR
	CLRF INDF ;COLOCA PORTC COMO OUTPUT
	BSF TRISB,0 ;COLOCA PORTB0 COMO INPUT
	BSF TRISB,4 ;COLOCA PORTB4 COMO INPUT
	BCF STATUS,RP0 
	BSF STATUS,C
	CLRF BUT
	MOVLW 26
	MOVWF CONT	
	BSF PORTA,0
	BSF INTCON,7  ;ATIVA GIE(GLOBAL)

MAIN:
	MOVFW DIGA
	CALL DIG
	MOVWF PORTD
	BSF PORTC, 0
	BCF PORTC,0	
	MOVFW DIGB
	CALL DIG
	MOVWF PORTD
	BSF PORTC, 1
	BCF PORTC, 1	
	MOVFW DIGC
	CALL DIG
	MOVWF PORTD
	BSF PORTC, 2
	BCF PORTC,2	
	MOVFW DIGD
	CALL DIG
	MOVWF PORTD
	BSF PORTC, 3
	BCF PORTC,3
	GOTO MAIN		


DIG	ADDWF	PCL,F	
DIG0	RETLW 0x3F
DIG1	RETLW 0x06
DIG2	RETLW 0x5b
DIG3	RETLW 0x4F
DIG4	RETLW 0x66
DIG5	RETLW 0x6D
DIG6	RETLW 0x7D
DIG7	RETLW 0x07
DIG8	RETLW 0x7F
DIG9	RETLW 0x6F